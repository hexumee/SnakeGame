FIND_PATH(SDL2_IMAGE_INCLUDE_DIR SDL_image.h
    HINTS
    ENV SDL2IMAGEDIR
    ENV SDL2DIR
    PATH_SUFFIXES SDL2
    include/SDL2 include
    PATHS ${SDL2_IMAGE_PATH}
)

IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
    SET(VC_LIB_PATH_SUFFIX lib/x64)
ELSE()
    SET(VC_LIB_PATH_SUFFIX lib/x86)
ENDIF()

FIND_LIBRARY(SDL2_IMAGE_LIBRARY
    NAMES SDL2_image
    HINTS
    ENV SDL2IMAGEDIR
    ENV SDL2DIR
    PATH_SUFFIXES lib ${VC_LIB_PATH_SUFFIX}
    PATHS ${SDL2_IMAGE_PATH}
)

IF(SDL2_IMAGE_INCLUDE_DIR AND EXISTS "${SDL2_IMAGE_INCLUDE_DIR}/SDL_image.h")
    FILE(STRINGS "${SDL2_IMAGE_INCLUDE_DIR}/SDL_image.h" SDL2_IMAGE_VERSION_MAJOR_LINE REGEX "^#define[ \t]+SDL_IMAGE_MAJOR_VERSION[ \t]+[0-9]+$")
    FILE(STRINGS "${SDL2_IMAGE_INCLUDE_DIR}/SDL_image.h" SDL2_IMAGE_VERSION_MINOR_LINE REGEX "^#define[ \t]+SDL_IMAGE_MINOR_VERSION[ \t]+[0-9]+$")
    FILE(STRINGS "${SDL2_IMAGE_INCLUDE_DIR}/SDL_image.h" SDL2_IMAGE_VERSION_PATCH_LINE REGEX "^#define[ \t]+SDL_IMAGE_PATCHLEVEL[ \t]+[0-9]+$")
    STRING(REGEX REPLACE "^#define[ \t]+SDL_IMAGE_MAJOR_VERSION[ \t]+([0-9]+)$" "\\1" SDL2_IMAGE_VERSION_MAJOR "${SDL2_IMAGE_VERSION_MAJOR_LINE}")
    STRING(REGEX REPLACE "^#define[ \t]+SDL_IMAGE_MINOR_VERSION[ \t]+([0-9]+)$" "\\1" SDL2_IMAGE_VERSION_MINOR "${SDL2_IMAGE_VERSION_MINOR_LINE}")
    STRING(REGEX REPLACE "^#define[ \t]+SDL_IMAGE_PATCHLEVEL[ \t]+([0-9]+)$" "\\1" SDL2_IMAGE_VERSION_PATCH "${SDL2_IMAGE_VERSION_PATCH_LINE}")
    SET(SDL2_IMAGE_VERSION_STRING ${SDL2_IMAGE_VERSION_MAJOR}.${SDL2_IMAGE_VERSION_MINOR}.${SDL2_IMAGE_VERSION_PATCH})
    UNSET(SDL2_IMAGE_VERSION_MAJOR_LINE)
    UNSET(SDL2_IMAGE_VERSION_MINOR_LINE)
    UNSET(SDL2_IMAGE_VERSION_PATCH_LINE)
    UNSET(SDL2_IMAGE_VERSION_MAJOR)
    UNSET(SDL2_IMAGE_VERSION_MINOR)
    UNSET(SDL2_IMAGE_VERSION_PATCH)
ENDIF()

SET(SDL2_IMAGE_LIBRARIES ${SDL2_IMAGE_LIBRARY})
SET(SDL2_IMAGE_INCLUDE_DIRS ${SDL2_IMAGE_INCLUDE_DIR})

INCLUDE(FindPackageHandleStandardArgs)

FIND_PACKAGE_HANDLE_STANDARD_ARGS(SDL2_image
    REQUIRED_VARS SDL2_IMAGE_LIBRARIES SDL2_IMAGE_INCLUDE_DIRS
    VERSION_VAR SDL2_IMAGE_VERSION_STRING)

SET(SDLIMAGE_LIBRARY ${SDL2_IMAGE_LIBRARIES})
SET(SDLIMAGE_INCLUDE_DIR ${SDL2_IMAGE_INCLUDE_DIRS})
SET(SDLIMAGE_FOUND ${SDL2_IMAGE_FOUND})

MARK_AS_ADVANCED(SDL2_IMAGE_LIBRARY SDL2_IMAGE_INCLUDE_DIR)

